defmodule ChangelogWeb.AuthController do
  use ChangelogWeb, :controller

  alias Changelog.{Person, Mailer}
  alias ChangelogWeb.Email

  plug RequireGuest, "before signing in" when action in [:new, :create]
  plug Ueberauth

  def new(conn, %{"auth" =>  %{"email" => email}}) do

    if person = Repo.get_by(Person, email: email) do
      person = Person.refresh_auth_token(person, Phoenix.Token.sign(conn, Application.get_env(:changelog, :auth_token_salt), person.id))
      Email.sign_in(person) |> Mailer.deliver_later
      render(conn, "new.html", person: person)
    else
      conn
      |> put_flash(:success, "You aren't in our system! No worries, it's free to join. ğŸ’š")
      |> redirect(to: person_path(conn, :join, %{email: email}))
    end
  end

  def new(conn, _params) do
    render(conn, "new.html", person: nil)
  end

  def create(conn, %{"token" => token}) do
    case Phoenix.Token.verify(conn, Application.get_env(:changelog, :auth_token_salt), token, max_age: 1800) do
      {:ok, id} ->
        user = Repo.get_by(User, id: id, auth_token: token)
        sign_in_and_redirect(conn, person, home_path(conn, :show))

      {:error, :expired} ->
        conn
        |> put_flash(:error, "Token expired.")
        |> render("new.html",  person: nil)

      {:error, :invalid} ->
        conn
        |> put_flash(:error, "Token invalid.")
        |> render("new.html",  person: nil)
    end
  end

  def delete(conn, _params) do
    conn
    |> delete_resp_cookie("_changelog_user")
    |> redirect(to: root_path(conn, :index))
  end

  def callback(conn = %{assigns: %{ueberauth_auth: auth}}, _params) do
    if person = Person.get_by_ueberauth(auth) do
      sign_in_and_redirect(conn, person, home_path(conn, :show))
    else
      conn
      |> put_flash(:success, "Almost there! Please complete your profile now.")
      |> redirect(to: person_path(conn, :join, params_from_ueberauth(auth)))
    end
  end

  def callback(conn = %{assigns: %{ueberauth_failure: _fails}}, _params) do
    conn
    |> put_flash(:error, "Something went wrong. ğŸ˜­")
    |> render("new.html", person: nil)
  end

  defp params_from_ueberauth(%{provider: :github, info: info}) do
    %{name: info.name, handle: info.nickname, github_handle: info.nickname}
  end

  defp params_from_ueberauth(%{provider: :twitter, info: info}) do
    %{name: info.name, handle: info.nickname, twitter_handle: info.nickname}
  end

  defp sign_in_and_redirect(conn, person, route) do
    Repo.update(Person.sign_in_changeset(person))

    conn
    |> assign(:current_user, person)
    |> put_flash(:success, "Welcome to Changelog!")
    |> put_encrypted_cookie("_changelog_user", person.id)
    |> configure_session(renew: true)
    |> redirect(to: route)
  end
end
